// d.hatena.mac
$mn = "はてなダイアリー用マクロ - ";
// copyright (C) taketori <taketori at x10d.jp>
// ライセンスは、いわゆる「修正BSD」です。
// 動作環境は、秀丸(Ver.8.00以上)・COM・cmd.exe・同梱JScriptのインストール・読み出し・実行ができ、編集中ファイルの読み出し・実行、および保存先のフォルダにて読み出し・書き込みができる 環境。
// 設定によっては、本マクロと同じフォルダにて、ファイルを作成・書き込みできること。
// 以上の詳細は、配布ファイルに同梱の<README.txt>を参照。本マクロのマニュアルは<d.hatena.mac.txt>。

$id = "";

INIT:

	// 設定ファイル、X-WSSEヘッダ生成スクリプトの設置場所を取得する。
	$$ini = currentmacrofilename + ".ini";			// 設定ファイル
	$$wssegen = currentmacrofilename + ".js";		// X-WSSEヘッダ生成スクリプト
	if(existfile($$ini) == false || existfile($$wssegen) == false){
		goto EXIT "以下のいずれかまたは両方のファイルがないので継続できません。\n\t" + $$ini + " (設定ファイル)\n\t" + $$wssegen + " (X-WSSEヘッダ生成スクリプト)\n配布ファイルに同梱の d.hatena.mac.ini.sample、d.hatena.mac.js.sample をコピー・改名してください");
	}
	call compile_js $$ini, $$wssegen;
	if($$return == "")	goto EXIT $$wssegen + "からコンパイルできませんでした。";
	$$wssegen = $$return;

MAIN:

	// X-WSSEデータを生成する。
	if($id == "")	$id = input($mn + "\nこのデータを、どのアカウントで投稿しますか。\nアカウント名を入力してください。");
	call runex_to $$wssegen +  " " + $id, 3, $$ini;
	if(##return)	goto EXIT "X-WSSEの値を" + $$ini + "に追加する際に失敗しました。\nアカウント名か" + $$ini + "の書き込み権限・排他設定を疑ってください。";

	// 投稿する内容を生成する。
	call make_data;

	// はてなに接続して投稿する。
	call connect_hatena "POST", "http://d.hatena.ne.jp/" + $id + "/atom/blog",
		member(##return, "xml"), $$ini;
	if(##return)	message midstr(member(member(##return, "selectSingleNode", "//id"), "text"), strlen("tag:d.hatena.ne.jp,2008:"), ) + " に投稿しました。";		//TODO: linkノードから取得する。
	else	message "failed";

EXIT:

	if($$1 != "")	message $mn + "\n" + $$1;

endmacro;

compile_js:
//arg: $1:設定ファイルのフルパス,  $2:X-WSSEヘッダ生成スクリプトのフルパス(存在確認済)
//ARG: $mn,  Registory
//ret: X-WSSEヘッダ生成実行ファイルのフルパス。(コンパイルに失敗したらマクロ終了)

	title $mn + $$2 + "を実行ファイルへコンパイルする準備をしています。";
	$$wssegen = leftstr($$2, strrstr($$2, ".js")) + ".exe";
	if(existfile($$wssegen))	call runex_to $$wssegen;		// 引数なしで起動したときはソースコードとの新旧比較。
	if(##return/*1=実行ファイルの方が新しい*/){
		title $mn + $$2 + "より実行ファイルの方が新しいので、コンパイルを中断しました。";
		return $$wssegen;
	}

	// コンパイルに利用するjsc.exeを指定する。
	$$jsc = getinistr($$1, currentmacrobasename, "jsc") + "\\jsc.exe";
	openreg "LOCALMACHINE", "SOFTWARE\\Microsoft\\.NETFramework";
	if(result){
		$$jsc = getregstr("InstallRoot") + $$jsc;
		closereg;
	}else{
		if(getinistr($$1, currentmacrobasename, "InstallRoot") == "")
			call runex_to "cmd.exe /C FOR /f \"skip=4 tokens=1,3\" %a IN ('reg.exe QUERY HKLM\\SOFTWARE\\Microsoft\\.NETFramework /v InstallRoot') DO @ECHO %a = %%b", 3, $$1;
		$$jsc = getinistr($$1, currentmacrobasename, "InstallRoot") + $$jsc;
	}
	if(!existfile($$jsc))	return;

	// コンパイルする。
	title $mn + "コンパイル中。少々お待ちを。:" +$$2 + " by " + $$jsc;
	call runex_to $$jsc + " /warnaserror+ /warn:0 /out:" + $$wssegen + " " + $$2;
	if(##return)	goto EXIT $$jsc + " を起動して " + $$2 + " をコンパイルする際に失敗しました。";

return $$wssegen;

runex_to:
// 引数で指定されたプログラムをrunexを用いて実行し、アウトプット枠に出力する。
//arg: $1:実行するプログラムのフルパス, #2:出力する場所, $3:#2がファイルの時のファイル名
//ret: 0=正常に起動でき終了コード0だった。0以外=プログラムの起動に失敗か終了コードが0以外。

	runex $$1, 1/*sync:sync*/, 0, ""/*stdin:none*/, ##2, $$3/*stdout:none*/, 0, ""/*stderr:none*/, 1, ""/*folder:current*/, 2/*show:hide*/, 0/*nodraw:draw*/, 0/*code:ansi*/;

return (result == false) + getresultex(9);

make_data:
// 編集中の秀丸から必要なデータを抽出し、投稿用のデータにする。
// データの形式は1行目がタイトル、2行目以降が本文。タイトル行の前にヘッダ行があればそれを使う("^updated:\s*"があればその日付。"^subject:\s*"に該当するものがあればその行をタイトル行にしてそれ以外のを本文に)。
//arg:
//ret: #取得したDOMDocumentオブジェクトの番号

	$$template = currentmacrofilename + ".xml";
	call get_dom 0, $$template;		// 失敗したらマクロ終了なので##returnには必ず値が入る。
	##dom = ##return;

	if(selecting || rectselecting){
		escape;
		moveto2 seltopcolumn, seltoplineno;
		if(seltoplineno == selendlineno){
			setpropstr member(##dom, "selectSingleNode", "//title"), "text", gettext2(seltopcolumn, seltoplineno, selendcolumn, selendlineno);
		}else{
			setpropstr member(##dom, "selectSingleNode", "//title"), "text", gettext2(seltopcolumn, seltoplineno, linelen2, seltoplineno);
			setpropstr member(##dom, "selectSingleNode", "//content"), "text", gettext2(0, seltoplineno + 1, selendcolumn, selendlineno);
		}
	}else{
		gofiletop;
		setpropstr member(##dom, "selectSingleNode", "//title"), "text", gettext2(column, lineno, linelen2, lineno);
		gofileend;
		setpropstr member(##dom, "selectSingleNode", "//content"), "text", gettext2(0, 2, column, lineno);
	}

return ##return;

connect_hatena:
// XMLHTTPのopenメソッドを呼び出したのち、取得したXMLをDOMDocumentでパースする。
//arg: $1:接続方法(POST/GET), $2:接続するURI, $3:sendの引数, $4:INIファイル名
//ret: get_dom(# "Msxml2.DOMDocument.3.0"オブジェクトの番号)。エラーの時はマクロ終了。

	// XMLを取得。
	$$src = "d.hatena.ne.jp";
	title $mn + $$1 + "のために接続 " + $$2;
	##xmlhttp = createobject("Msxml2.XMLHTTP.3.0");
	if(!##xmlhttp)	##xmlhttp = createobject("Microsoft.XMLHTTP");
	if(!##xmlhttp)	goto EXIT "XMLHTTPオブジェクトにアクセスできません。";

	callmethod ##xmlhttp, "Open", $$1, $$2, 0;
	callmethod ##xmlhttp, "setRequestHeader", "X-WSSE", getinistr($$4, currentmacrobasename, "x-wsse");
	callmethod ##xmlhttp, "setRequestHeader", "Content-Type", "text/xml";
	callmethod ##xmlhttp, "send", $$3;
	title $mn + "XMLHTTP result = " + str(getpropnum(##xmlhttp, "status")) + ":" +  getpropstr(##xmlhttp, "statusText");
	writeinistr $$ini, currentmacrobasename, "x-wsse", 0;

	if(300 <= getpropnum(##xmlhttp, "status"))	goto EXIT $$src + " に" + $$1 + "できません。";
	call get_dom ##xmlhttp, $$src;

return ##return;

get_dom:
// DOMオブジェクトの確保。
//arg: #1:XMLHTTPオブジェクトの番号(0=$2をファイル名と見なしその内容を用いる), $2:接続ドメイン(#1=0以外) / 開くファイルのフルパス(#1=0)
//ARG: $mn
//ret: # "Msxml2.DOMDocument.3.0"オブジェクトの番号。エラーの時はマクロ終了。

	title $mn + $$2 + "にあったXMLを解析しています。";
	##dom = createobject("Msxml2.DOMDocument.3.0");
	if(!##dom)	goto EXIT "Msxml2.DOMDocument.3.0が使えない環境のため、続行できません。";
	setpropnum ##dom, "async", 0;

	// XML解析
	if(##1){
		//NOTE: 「member(member(##1,"responseXML"),"xml")」ではGET ID/atom/blogの時解析に失敗する。また「member(##1,"responseXML")」では##domに対してプロパティ・メソッドが発行できない。
		callmethod ##dom, "loadXML", getpropstr(##1, "responseText");
	}else{
		callmethod ##dom, "load", $$2;
	}
	if(member(member(##dom, "parseError"), "errorCode") != 0){
		goto EXIT $$2 + " から取得したXMLファイルの解析に失敗しました:\n" + member(member(##dom, "parseError"), "reason");
	}

return ##dom;

