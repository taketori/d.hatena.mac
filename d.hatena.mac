// d.hatena.mac
$mn = "はてなダイアリー用マクロ - ";
// copyright (C) taketori <taketori at x10d.jp>
// ライセンスは、いわゆる「修正BSD」です。
// 動作環境は、秀丸(Ver.8.00以上)・COM・cmd.exe・同梱JScriptのインストール・読み出し・実行ができ、編集中ファイルの読み出し・実行、および保存先のフォルダにて読み出し・書き込みができる 環境。
// 設定によっては、本マクロと同じフォルダにて、ファイルを作成・書き込みできること。
// 以上の詳細は、配布ファイルに同梱の<README.txt>を参照。本マクロのマニュアルは<d.hatena.mac.txt>。

$id = "";

// 実行できない条件の時は、さっさと終了する。
if( version < 801 ){
	message "秀丸のバージョンが古いです。" + currentmacrobasename + "を終了します。";
	endmacro;
}

// 初期設定
	#_[0] = x;	#_[1] = y;
	$_[0] = searchbuffer;	#_[2] = searchoption;	$_[1] = getsearchhist(0);
	#_[3] = foundhilighting;	$_[2] = foundbuffer;	#_[4] = foundoption;
	#_[5] = overwrite;	#_[6] = browsemode;	#_[7] = readonly;	#_[8] = imestate;	#_[9] = freecursor;
	if( #_[5] )	overwriteswitch;
	if( #_[6] )	browsemodeswitch;
	if( #_[7] )	readonlyswitch;
	if( #_[8] )	imeswitch;
	if( #_[9] )	freecursorswitch;
setcompatiblemode 0x0002 | 0x0008 | 0x0200;		// 0x002=折りたたみを無視ししてなるべく維持, 0x0008=部分編集を無視してなるべく維持, 0x0200=検索での表示を「範囲選択」。

INIT:

	// 設定ファイル、本マクロ用実行ファイルの設置場所を取得する。
	$$ini = currentmacrofilename + ".ini";			// 設定ファイル
	if(!existfile($$ini)){
		call EXIT "\t" + $$ini + " (設定ファイル)\nがないので継続できません。\n配布ファイルに同梱の d.hatena.mac.ini.sampleをコピーし上記に改名してください";
	}
	$$dll_src = currentmacrofilename + ".Settings.cs";
	$$dll     = currentmacrofilename + ".util.dll";
	$$exe     = currentmacrofilename + ".exe";
	call compile_src $$dll_src, $$dll, $$exe;
	if(##return)	call EXIT $$dll + ", " + $$exe + "のコンパイルを中止しました";

MAIN:

	##char = inputchar($mn + getstaticvariable("x10d_" + currentmacrobasename + "_user", 0) + ": Press [P]ost, [G]et, [M]odify, [D]elete or [H]elp.");
	if     (##char == 'p')	call post $$ini;
	else if(##char == 'g')	call get_entry $$ini;
	else if(##char == 'm')	call mod_entry $$ini;
	else if(##char == 'd')	call del_entry $$ini;
	else if(##char == 'h')	call disp_help;
	else 										call disp_help;

EXIT:

	if($$1 != "")	message $mn + "\n" + $$1;

	escape;
	moveto #_[0], #_[1];
	if( #_[5] )	overwriteswitch;
	if( #_[6] )	browsemodeswitch;
	if( #_[7] )	readonlyswitch;
	if( #_[8] )	imeswitch;
	if( #_[9] )	freecursorswitch;

endmacro;

disp_help:

	message $mn + "\n"
	      + "使い方:\n"
	      + "マクロを起動してから、[ ]内のいずれかのキーを押下する。\n"
	      + "[P]=投稿、 [G]=取得、[M]=修正、[D]=消去、[H]=ヘルプ(この画面の表示)\n"
	      + "\n"
	      + "投稿:\t範囲選択をしておくと、その範囲の1行目をタイトルに、2行目以降を本文にします。\n"
	      + "\t範囲選択していない場合は、編集中の文章すべてを対象にします。\n"
	      + "修正:\t用いるデータは「投稿」と同様です。データは事前に「取得」しておくことをおすすめします。\n"
	      + "\n"
	      + "「date: 日付」「blog: はてなのid」で、日記の日付、投稿に使うユーザ名などを設定できる。(データ形式は適当な日記を取得して確認してください。)\n";

return 0;

post:
//arg: $1:iniファイル

	// 投稿する内容を生成する。
	call make_data;
	##dom = ##return;

	// はてなに接続して投稿する。
	$$uri = getpropstr(member(##dom, "selectSingleNode", "//uri"), "text");
	if($$uri == "")	$$uri = "http://d.hatena.ne.jp/%user%/atom/blog";
	call connect_hatena "POST", $$uri, ##dom, $$1;		//失敗の時はマクロ終了。
	##dom = ##return;
	/*NOTE: このマクロでは以下のような形式であることを前提に作っている。
	<link rel="edit" href="〜"/><link rel="alternate" 〜/>
	要するに、linkノードはrel=editを持つもの → rel=alternateを持つもの の順番である。*/
	title $mn + "投稿完了";
	##link = member(##dom, "selectSingleNode", "//link");
	if(member(##link, "getAttribute", "rel") == "edit"){
		message $mn + "\n" + member(##link, "getAttribute", "href") + "\nに投稿しました。";
	}else{
		message $mn + "\n" + midstr(member(member(##dom, "selectSingleNode", "//id"), "text"), strlen("tag:d.hatena.ne.jp,2008:"), ) + "\nに投稿しました。";
	}

return 0;

get_entry:
//arg: $1:iniファイル

	call get_list $$1, 1, "表示する";
	$$uri = $$return;
	call connect_hatena "GET", $$uri, 0, $$1;		//失敗の時はマクロ終了。
	##dom = ##return;
	title $mn;
	question $mn + "\n以下のデータをカーソル部分に挿入します。\n[はい] = 挿入。[いいえ] = ダイアログで表示。\n\n" + $$uri;
	$$msg = midstr("blog: draft:", (strstr($$uri, "/atom/blog/") == -1) * 6, 6)		// $$uri中に"blog"がない場合は、$$uriのヘッダを"draft"にする。
	      + $$uri + "\n"
	      + "updated: " + member(member(##dom, "selectSingleNode", "//updated"), "text") + "\n"
	      + member(member(##dom, "selectSingleNode", "//title"), "text") + "\n"
	      + member(member(##dom, "selectSingleNode", "//hatena:syntax"), "text");
	if(result)	insert $$msg;
	else				message $$msg;

return 0;

mod_entry:
//arg: $1:iniファイル

	// 投稿する内容を生成する。
	call make_data;
	##dom = ##return;
	$$uri = getpropstr(member(##dom, "selectSingleNode", "//uri"), "text");
	if($$uri == ""){
		call get_list $$1, 1, "修正する";
		$$uri = $$return;
	}

	question $mn + "修正していいですか。";
	if(!result)	return;
	call connect_hatena "PUT", $$uri, ##dom, $$1;		//失敗の時はマクロ終了。

	// 投稿に成功。
	title $mn + "投稿完了";
	message $mn + "\n" + member(member(##return, "selectSingleNode", "//link"), "getAttribute", "href") + "\nを修正しました。";

return 0;

del_entry:
//arg: $1:iniファイル

	call make_data;
	$$uri = getpropstr(member(##return, "selectSingleNode", "//uri"), "text");
	if($$uri == ""){
		call get_list $$1, 1, "削除する";
		$$uri = $$return;
	}
	title $mn;
	question $mn + "本当に削除しますか。\n" + $$uri;
	if(!result)	return;
	call connect_hatena "DELETE", $$uri, 0, $$1;		//失敗の時はマクロ終了。
	message $mn + "\n削除しました。";

return 0;

get_list:
//arg: $1:iniファイル, #2:ページ数, $3:何の対象か
//ret: $選択したエントリのURI

	// 2ページ以降の時は、前のページへの案内をつける。
	##i = 0;
	if(1 < ##2){
		$$menu[##i] = "&b <--- 前の20件 ---";
		##i = ##i + 1;
	}

	// 現在のページの20件を取得する。
	$$_ = "asdfghjklqwertyuiopvm";		// メニューの頭につけるあれ
	call connect_hatena "GET",
	                    "http://d.hatena.ne.jp/%user%/atom/blog?page=" + str(##2),
	                    0, $$1;		//失敗の時はマクロ終了。
	##entrys = member(##return, "selectNodes", "//entry"); //NOTE: not ##entries
	##items = member(##entrys, "length");
	while(##i < ##items + (##2 != 1) ){
		##entry = getcollection(##entrys);
		$$menu[##i] = "&" + midstr($$_, ##i, 1) + " " + member(member(##entry, "selectSingleNode", "title"), "text");
		$$uri[##i] = member(member(##entry, "selectSingleNode", "link"), "getAttribute", "href");
		##i = ##i + 1;
	}

	// このページで20件あるときは次のページもあると見なして、次のページへの案内をつける。
	if(##items == 20){
		$$menu[##i] = "&n --- 次の20件 --->";
		##i = ##i + 1;
	}

	// メニューを表示する。
	title $mn + $$3 + "日記のタイトルをメニューの中から選択してください。";
	menuarray $$menu, ##i;
	if(result == 0)											call EXIT;
	if(result == 1 && ##2 != 1)		call get_list $$1, ##2 - 1;
	if(result == 21 + (##2 != 1))	call get_list $$1, ##2 + 1;
	else return $$uri[result - 1];

return $$return;

compile_src:
//arg: $1:DLLのソース, $2:DLLのフルパス, $3:実行ファイルのフルパス
//ARG: $mn,  Registory
//ret: #コンパイルの結果(0=成功, 0以外=失敗)

	title $mn + "コンパイルする準備をしています。";
	if(existfile($$1) == false && existfile($$2) == false){
		call EXIT "\t" + $$dll_src + " (本マクロ用実行ファイルの	DLLソース)\nがないので継続できません。\n配布ファイルに同梱の d.hatena.mac.Settings.cs.sample をコピーし上記に改名してください。\n\n　このファイルにユーザID(とさらに任意でパスワード)を記載しておくと、複数のはてなダイアリーを使い分けることができます。\n　詳細は上記のファイルをご覧ください。";
	}
	if(existfile($$2) && existfile($$3)){
		call runex_to $$3 + " compare";		// ソースコードとDLLとの新旧比較。
		if(##return/*1=DLLの方が新しいかソースがない。*/){
			title $mn + "コンパイルしませんでした。";
			return 0;
		}
	}

	// .NET Frameworkのインストールディレクトリを推定してコンパイルする。
	//NOTE: 持ち出しキットを利用しているときは、秀丸のopenregの結果がfalseになるので使えない。またVer2以上の.NET Frameworkでないと面倒くさいことになるのでスクリプトでもできない。
	//NOTE: これ以上長いコマンドラインでは、外部プログラムが実行されない。
	title $mn + $$2 + "を実行ファイルへコンパイルしています。";
	call runex_to getenv("ComSpec") + " /C "
	  // %r=.NET Frameworkのインストールフォルダ を取得して代入する。
	  + "FOR /F \"skip=4 tokens=3\" %r IN ('reg.exe QUERY HKLM\\SOFTWARE\\Microsoft\\.NETFramework /v InstallRoot') "
	  // フォルダ名で降順に列挙し、%c=csc.exeへのフルパス として代入する。
	  + "DO @( FOR /F \"delims= \" %c IN ('dir /O:-N /S /B %r\\csc.exe') "
	  // %cを利用してコンパイル。失敗したら順次古いバージョンでコンパイル。
	       + "DO @( %c /warnaserror+ /w:4 /t:library /out:" + $$2 + " " + $$1 + " "
	          + "&& %c /warnaserror+ /w:4 /t:winexe /R:" + $$2 + " /out:" + $$3 + " " + currentmacrodirectory + "\\src\\*.cs"
	  // コンパイルできたら結果を表示する。
	          + "&& exit 0) )",
	  7, "";

return ##return;

runex_to:
// 引数で指定されたプログラムをrunexを用いて実行し、アウトプット枠に出力する。
//arg: $1:実行するプログラムのフルパス, #2:出力する場所, $3:#2がファイルの時のファイル名
//ret: 0=正常に起動でき終了コード0だった。0以外=プログラムの起動に失敗か終了コードが0以外。

	runex $$1, 1/*sync:sync*/, 0, ""/*stdin:none*/, ##2, $$3/*stdout*/, 0, ""/*stderr:none*/, 1, ""/*folder:current*/, 2/*show:hide*/, 0/*nodraw:draw*/, 0/*code:ansi*/;

return (result == false) + getresultex(9);

make_data:
// 編集中の秀丸から必要なデータを抽出し、投稿用のデータにする。
//arg: --
//ret: #取得したDOMDocumentオブジェクトの番号

	$$make_data_mac = currentmacrofilename + ".makedata.mac";
	if(!existfile($$make_data_mac))	call EXIT $$make_data_mac + "がないので続行できません。";
	execmacro $$make_data_mac, $mn, currentmacrofilename;
	if(3 <= strlen(getresultex(-1)))	call EXIT getresultex(-1);		// 3文字以上ならたぶんエラーメッセージ。
	##ret = val(getresultex(-1));		// 3文字以下ならたぶん取得したDOMDocumentオブジェクトの番号。

return ##ret;

connect_hatena:
// XMLHTTPのopenメソッドを呼び出したのち、取得したXMLをDOMDocumentでパースする。
//arg: $1:接続方法(POST/GET等), $2:接続するURI, #3:オブジェクトの番号(sendの引数), $4:INIファイル名
//ret: #:get_dom("Msxml2.DOMDocument.3.0"オブジェクトの番号)。$1が"DELETE"の時はレスポンス番号(200)。エラーの時はマクロ終了。
//NOTE: 第3引数のオブジェクトは、この関数で破棄される。

	// ユーザ名を設定する。
	call set_user ##3;
	if($$return == "")	call EXIT "ユーザ名かパスワードが指定されていません。";

	// URI中の%user%をユーザ名に置き換える。
	$$uri = $$2;
	##p = strstr($$2, "%user%");
	if(0 < ##p){
		$$uri = leftstr($$2, ##p) + $$return + midstr($$2, ##p + 6, );
	}

	// 接続するための準備。
	title $mn + $$1 + "のために接続 " + $$uri;
	##xmlhttp = createobject("Msxml2.XMLHTTP.3.0");
	if(!result)	call EXIT "Msxml2.XMLHTTP.3.0が使えない環境のため、続行できません。";
	callmethod ##xmlhttp, "open", $$1, $$uri, 0;
	callmethod ##xmlhttp, "setRequestHeader", "X-WSSE", getinistr($$4, currentmacrobasename, "x-wsse");
	callmethod ##xmlhttp, "setRequestHeader", "Content-Type", "text/xml";
	// キャッシュ対策。
	callmethod ##xmlhttp, "setRequestHeader", "If-Modified-Since", "Thu, 01 Jun 1970 00:00:00 GMT";
	if(getinistr($$4, currentmacrobasename, "Cache-Control") != ""){
		callmethod ##xmlhttp, "setRequestHeader", "Cache-Control",
		           getinistr($$4, currentmacrobasename, "Cache-Control");
	}
	if(getininum($$4, currentmacrobasename, "Pragma")){
		callmethod ##xmlhttp, "setRequestHeader", "Pragma", "no-cache";
	}
	// 実際に接続する。
	title $mn + $$1 + "を試しています。しばらくお待ちください。 " + $$uri;
	if(##3)	callmethod ##xmlhttp, "send", member(##3, "xml");
	else 		callmethod ##xmlhttp, "send", "";
	title $mn + $$1 + "を完了しました。" + $$uri;
	writeinistr $$4, currentmacrobasename, "x-wsse", 0;
	writeinistr $$4, currentmacrobasename, "user", 0;

	// 結果判定。
	##status = getpropnum(##xmlhttp, "status");
	title $mn + "XMLHTTP result = " + str(##status) + ":" +  getpropstr(##xmlhttp, "statusText");
	if(300 <= ##status)	call EXIT $$1 + "できません。: " + $$uri;
	if($$1 == "DELETE")	return ##status;
	call get_dom ##xmlhttp, $$uri;

return ##return;

get_dom:
// DOMオブジェクトの確保。
//arg: #1:XMLHTTPオブジェクトの番号(0=$2をファイル名と見なしその内容を用いる), $2:接続ドメイン(#1=0以外) / 開くファイルのフルパス(#1=0)
//ARG: $mn
//ret: # "Msxml2.DOMDocument.3.0"オブジェクトの番号。エラーの時はマクロ終了。

	title $mn + "XMLを解析しています : " + $$2;
	##dom = createobject("Msxml2.DOMDocument.3.0");
	if(!result)	call EXIT "Msxml2.DOMDocument.3.0が使えない環境のため、続行できません。";
	setpropnum ##dom, "async", 0;

	// XML解析
	if(##1){
		//NOTE: 「member(member(##1,"responseXML"),"xml")」ではGET ID/atom/blogの時解析に失敗する。また「member(##1,"responseXML")」では##domに対してプロパティ・メソッドが発行できない。
		callmethod ##dom, "loadXML", getpropstr(##1, "responseText");
	}else{
		callmethod ##dom, "load", $$2;
	}
	if(member(member(##dom, "parseError"), "errorCode") != 0){
		call EXIT $$2 + " から取得したXMLファイルの解析に失敗しました:\n" + member(member(##dom, "parseError"), "reason");
	}
	title $mn + "XMLの解析を完了しました。: " + $$2;

return ##dom;

set_user:
// ユーザ名を設定する。パスワードが取得できたらX-WSSEを設定する。
//arg:DOMDocumentオブジェクトの番号
//ret:$ユーザ名

	// 既存設定を参照する。
	title $mn + "ユーザ名・パスワードを設定しています。";
	$$user = getpropstr(member(##1, "selectSingleNode", "//user"), "text");
	if($$user == "")	$$user = getstaticvariable("x10d_" + currentmacrobasename + "_user", 0);
	if($$user == "")	$$user = $id;

	// 実行ファイルでユーザ名を設定/確認する。
	$$exe = currentmacrofilename +  ".exe";
	call runex_to $$exe +  " user " + $$user;
	$$user = getinistr(currentmacrofilename +  ".ini", currentmacrobasename, "user");
	if(##return){		// パスワードが設定されていない。
		if($$user == "")	return "";
		$$pw = input($mn + "\nはてなダイアリーのパスワードを入力してください。\nユーザ名:" +  $$user, "password");
		if($$pw == "")	return "";
		call runex_to $$exe +  " user " + $$user + " " + $$pw;
	}else	setstaticvariable "x10d_" + currentmacrobasename + "_user", $$user, 0;

return $$user;
